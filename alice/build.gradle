plugins {
    id 'java-library'
    id 'maven-publish'
    id 'groovy'
    id 'to.wetransform.semantic-release-version' version '2.1.3'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

/*
 * Group and version. Project/service name is configured in settings.gradle.
 */
group = 'to.wetransform'

dependencies {
    implementation "org.codehaus.groovy:groovy:2.5.23"

    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation group: 'commons-io', name: 'commons-io', version: '2.21.0'
    testImplementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.20.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.14.2'
    testRuntimeOnly group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.14.2'
}

configurations.all {
    // ensure SNAPSHOTs are updated every time if needed
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

configurations {
    // activate dependency locks for lockfile generation (used by trivy security scan)
    compileClasspath {
        resolutionStrategy.activateDependencyLocking()
    }
    runtimeClasspath {
        resolutionStrategy.activateDependencyLocking()
    }
}

/*
 * Packaging and publishing
 */

// package javadoc into a jar file
task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    archiveClassifier = 'javadoc'
}

// package source into a jar file
task packageSources(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

// define artifacts for upload
artifacts {
    archives jar
    archives packageJavadoc
    archives packageSources
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact packageSources
            artifact packageJavadoc
        }
    }

    repositories {
        maven {
            url = project.version.endsWith('-SNAPSHOT') ?
                'https://artifactory.wetransform.to/artifactory/libs-snapshot-local' :
                'https://artifactory.wetransform.to/artifactory/libs-release-local'
            credentials {
                username project.hasProperty('wetfArtifactoryUser') ? wetfArtifactoryUser : ''
                password project.hasProperty('wetfArtifactoryPassword') ? wetfArtifactoryPassword : ''
            }
        }
    }
}
